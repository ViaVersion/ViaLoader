plugins {
    id "java-library"
    id "maven-publish"
    id "net.raphimc.class-token-replacer" version "1.0.0"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    group = project.maven_group ?: rootProject.maven_group
    archivesName = project.maven_name ?: rootProject.maven_name
    version = project.maven_version ?: rootProject.maven_version
}

repositories {
    mavenCentral()
    maven {
        name = "Lenni0451"
        url "https://maven.lenni0451.net/everything"
    }
    maven {
        name = "OpenCollab Releases"
        url = "https://repo.opencollab.dev/maven-releases/"
    }
    maven {
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/maven-snapshots/"
    }
    maven {
        name = "ViaVersion"
        url "https://repo.viaversion.com"
    }
    maven {
        name = "Minecraft Libraries"
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    compileOnly "com.viaversion:viaversion:4.9.0-23w40a-SNAPSHOT"
    compileOnly("com.viaversion:viabackwards-common:4.9.0-23w40a-SNAPSHOT") {
        exclude group: "com.viaversion", module: "viaversion"
        exclude group: "io.netty", module: "netty-all"
    }
    compileOnly "com.viaversion:viarewind-common:3.0.3-SNAPSHOT"
    compileOnly "net.raphimc:ViaLegacy:2.2.21-SNAPSHOT"
    compileOnly "net.raphimc:ViaAprilFools:2.0.10-SNAPSHOT"
    compileOnly "net.raphimc:ViaBedrock:0.0.3-SNAPSHOT"
    compileOnly "org.cloudburstmc.netty:netty-transport-raknet:1.0.0.CR1-SNAPSHOT"

    api "org.slf4j:slf4j-api:2.0.9"
    api "org.yaml:snakeyaml:2.2"
}

sourceSets {
    main {
        classTokenReplacer {
            property("\${version}", project.version)
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.name ?: rootProject.name}" }
    }
}

publishing {
    repositories {
        maven {
            name = "Via"
            url = "https://repo.viaversion.com/"

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.maven_name
            version = project.maven_version

            from components.java

            pom {
                name = "ViaLoader"
                description = "Implementation of a ViaVersion based protocol translator platform"
                url = "https://github.com/ViaVersion/ViaLoader"
                licenses {
                    license {
                        name = "GPL-3.0 License"
                        url = "https://github.com/ViaVersion/ViaLoader/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "RK_01"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/ViaVersion/ViaLoader.git"
                    developerConnection = "scm:git:ssh://github.com/ViaVersion/ViaLoader.git"
                    url = "https://github.com/ViaVersion/ViaLoader.git"
                }
            }
        }
    }
}
